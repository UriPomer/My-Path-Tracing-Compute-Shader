#pragma kernel CSMain

#include "struct.hlsl"

RWTexture2D<float4> _Result;
float2 _Resolution;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;
float3 _DirectionalLight;
float4 _DirectionalLightColor;

static const float PI = 3.14159265f;

float sdot(float3 x, float3 y, float f = 1.0f)
{
    return saturate(dot(x, y) * f);
}

float energy(float3 color)
{
    return dot(color, 1.0f / 3.0f);
}


float2 _Pixel;
float _Seed;

float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f); // Fraction part
    _Seed += 1.0f;
    return result;
}

RWStructuredBuffer<Sphere> _Spheres;

RayHit HitSphere(Sphere sphere, Ray r)
{
    float3 center = sphere.position;
    float radius = sphere.radius;
    
    float3 oc = center - r.origin;
    float a = dot(r.dir, r.dir);
    float h = dot(r.dir, oc);
    float c = dot(oc, oc) - radius * radius;
    float discriminant = h * h - a * c;

    RayHit hit;
    
    if(discriminant < 0)
    {
        hit.position = float3(0, 0, 0);
        hit.distance = -1.0;
        return hit;
    }
    
    float t = (h - sqrt(discriminant)) / a;
    if(t < 0)
    {
        t = (h + sqrt(discriminant)) / a;
    }

    hit.position = r.origin + t * r.dir;
    hit.normal = normalize(hit.position - center);
    hit.mat = sphere.mat;
    hit.distance = t;
    return hit;
}

RayHit HitPlane(Plane plane,  Ray r)
{
    float t = dot(plane.p - r.origin, plane.normal) / dot(r.dir, plane.normal);
    RayHit hit;
    
    hit.normal = plane.normal;
    hit.mat = plane.mat;
    if(t < 0)
    {
        hit.position = float3(0, 0, 0);
        hit.distance = -1.0;
        return hit;
    }
    hit.position = r.origin + t * r.dir;
    hit.distance = t;
    
    return hit;
}

float3 RayAt(Ray r, float t)
{
    return r.origin + t * r.dir;
}

Ray GenRayByID(float2 pixelCoord)
{
    float2 screenPos = (pixelCoord + 0.5f) / _Resolution;
    float2 ndcPos = screenPos * 2.0f - 1.0f;

    float3 direction = mul(_CameraInverseProjection, float4(ndcPos, 0.0f, 1.0f)).xyz;
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);

    Ray ray;
    ray.origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    ray.dir = direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);

    return ray;
}

RayHit GenRayHit()
{
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.mat.albedo = float3(0.0f, 0.0f, 0.0f);
    hit.mat.specular = float3(0.0f, 0.0f, 0.0f);
    hit.mat.smoothness = 0.0f;
    hit.mat.emission = float3(0.0f, 0.0f, 0.0f);
    return hit;
}

RayHit Trace(Ray r)
{
    uint numSpheres, stride;
    _Spheres.GetDimensions(numSpheres, stride);
    RayHit hit = GenRayHit();
    for(uint i = 0; i < numSpheres; i++)
    {
        Sphere sphere = _Spheres[i];
        RayHit tempHit = HitSphere(sphere, r);
        if(tempHit.distance > 0.0 && tempHit.distance < hit.distance)
        {
            hit = tempHit;
        }
    }
    return hit;
}

float SmoothnessToPhongAlpha(float s)
{
    return pow(1000.0f, s * s);
}

float3x3 GetTangentSpace(float3 normal)
{
    // Choose a helper vector for the cross product
    float3 helper = float3(1, 0, 0);
    if (abs(normal.x) > 0.99f)
        helper = float3(0, 0, 1);

    // Generate vectors
    float3 tangent = normalize(cross(normal, helper));
    float3 binormal = normalize(cross(normal, tangent));
    return float3x3(tangent, binormal, normal);
}

float3 SampleReflectionDirectionSphere(float3 normal, float alpha)
{
    // Spherical coordinate to cartesian
    float cosTheta = pow(rand(), 1.0f / (alpha + 1.0f));
    float sinTheta = sqrt(1.0f - cosTheta * cosTheta);
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

    // Transform direction to world space
    return mul(tangentSpaceDir, GetTangentSpace(normal));
}

float3 Shade(inout Ray ray, RayHit hit)
{
    if(hit.distance < 1.#INF)
    {
        // Calculate chances of diffuse and specular reflection
        hit.mat.albedo = min(1.0f - hit.mat.specular, hit.mat.albedo);
        float specChance = energy(hit.mat.specular);
        float diffChance = energy(hit.mat.albedo);

        float roulette = rand();
        if(roulette < specChance)
        {
            // Specular reflection
            ray.origin = hit.position + hit.normal * 0.001f; // Offset the origin slightly to avoid self-intersection
            float alpha = SmoothnessToPhongAlpha(hit.mat.smoothness);
            float3 reflected = reflect(ray.dir, hit.normal);
            ray.dir = SampleReflectionDirectionSphere(reflected, alpha);
            
            ray.energy *= (1.0f / specChance) * hit.mat.specular * sdot(hit.normal, ray.dir, (alpha + 2) / (alpha + 1));
        }
        else if(diffChance > 0 && roulette < specChance + diffChance)
        {
            // Diffuse reflection
            ray.origin = hit.position + hit.normal * 0.001f;
            ray.dir = SampleReflectionDirectionSphere(hit.normal, 1.0f);
            ray.energy *= (1.0f / diffChance) * hit.mat.albedo;
        }
        else
        {
            ray.energy = float3(0.0f, 0.0f, 0.0f);
        }

        return hit.mat.emission;
    }
    else
    {
        ray.energy = float3(0.0f, 0.0f, 0.0f);
        // Sample the skybox and write it
        float theta = acos(ray.dir.y) / -PI;
        float phi = atan2(ray.dir.x, -ray.dir.z) / -PI * 0.5f;
        return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
    }
}

[numthreads(8,8,1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    Ray r = GenRayByID(id.xy);
    float3 result = float3(0.0f, 0.0f, 0.0f);
    for (int i = 0; i < 10; i++)
    {
        RayHit hit = Trace(r);
        float3 color = Shade(r, hit)  * r.energy;
        result += color;

        if(!any(r.energy))
        {
            break;
        }
    }

    _Result[id.xy] = float4(result, 1.0f);
}

